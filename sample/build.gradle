plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.50'
}

def compilerPlugin = project(':compiler-plugin')
def compilerPluginNative = project(':compiler-plugin-native')

evaluationDependsOn(":$compilerPlugin.name")
evaluationDependsOn(":$compilerPluginNative.name")

repositories {
    mavenCentral()
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions {
                def pluginJar = new File(compilerPlugin.buildDir, "libs/${compilerPlugin.name}-${compilerPlugin.version}.jar")
                freeCompilerArgs = ["-Xplugin", pluginJar.absolutePath]
            }
        }
    }
    macosX64("macos") {
        compilations.all {
            kotlinOptions {
                def pluginJar = new File(compilerPluginNative.buildDir, "libs/${compilerPluginNative.name}-${compilerPluginNative.version}.jar")
                freeCompilerArgs = ["-Xplugin", pluginJar.absolutePath]
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation project(":runtime")
            }
        }

        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
            }
        }
    }
}

afterEvaluate {
    tasks["compileKotlinJvm"].dependsOn += compilerPlugin.tasks["jar"]
    tasks["compileKotlinMacos"].dependsOn += compilerPluginNative.tasks["jar"]
}

